#### 1.3 用高阶函数做抽象

过程也是一类抽象，在常用的程序结构中，大部分情况是对数据进行处理，这一节来研究对过程处理和抽象。一些过程可以直接以过程为参数、或返回一些过程，理解这些，可以建立更加有力的抽象机制。

* 抽取方法本身，可以更一般化的表示过程
* lambda语法：`(lambda (augments) (procedure))`
* let语法：`(let ((<var_i> <exp_i>)) <body>)`
* lambda是建立局部的匿名函数，let是创建局部变量，都符合“闭包”的**即用即弃**原则
* 过程不仅可以当做参数传递，也可以当做返回值（更加精彩的是，在函数式语言的设计理念中，返回值的类别是没有区分的，也就是，返回一个函数和返回一个值没有区别）

部分习题：

1.30 过程sum的迭代版本。

```rkt
; recursion
(define (sum term a next b)
  (if (> a b) 0
      (+ (term a)
         (sum term (next a) next b))))

; iteration
(define (sum_iter term a next b)
  (define (iter a result)
    (if (> a b) 
         result
         (iter (next a) (+ (term a) result))))
    (iter a 0))
```

1.32 sum和product可以更高阶的抽象为accumulate。描述accumulate，并用它来重写sum和product。

sum和product的不同在于合并方式`combiner`和初始值`null-value`。accumulate过程需要添加这两个参数。

```rkt
(define (accumulate combiner null_value term a next b)
  (if (> a b) 
       null_value
       (combiner (term a)
                 (accumulate combiner null_value term (next a) next b))))
```

有了这个过程，重写sum和product就是定义不同的合并方式`combiner`和初始值`null-value`了：

```rkt
(define (sum term a next b)
  (accumulate + 0 term a next b))

(define (product term a next b)
  (accumulate * 1 term a next b))
```

1.33 更加高级的，可以为accumulate增加一个filter，过滤掉不需要的值。

```rkt
(define (accumulate_filter combiner filter null_value term a next b)
  (if (> a b) 
       null_value
       (combiner (if (filter (term a)) (term a) null_value)
                 (accumulate_filter combiner filter null_value term (next a) next b))))
```

注意这里的filter是用来过滤`(term a)`。

1.34 如果定义

```rkt
(define (f g)
   (g 2))
```

那么，解释器在解释`(f f)`时，会发生什么情况。

代换`(f f)`，可以得到`(f 2)`，这是会把2作为函数传递一个参数，解释器会报错“not a procedure”。

1.37 编写计算连分数的一个过程，分为递归版和迭代版。

```rkt
; recursion
(define (cont_frac n d k)
  (define (cont_frac_val i)
    (if (> i k)
        0
        (/ (n i) (+ (d i) (cont_frac_val (+ i 1))))))
  (cont_frac_val 1))

; iteration
(define (cont_frac_iter n d k)
  (define (cont_frac_val i result)
    (if (= i 0) 
        result
        (cont_frac_val (- i 1) (/ (n i) (+ (d i) result)))))
  (cont_frac_val k 0))
```

这里传递计算`n`和`d`的函数以及项数k的值，就可以计算连分数的值了。

可以发现，递归版和迭代版没有本质上的不同，无非就是迭代版的保存了一个当前的result，并且和递归版的顺序相反，所以后续将不再区分它们。

1.41~43 抽象过程double，接受一个过程，返回一个过程，这个过程将原来的参数过程运用两次。假设接受过程的参数只能有一个。

类似的，抽象过程compose和repeat。

```rkt
(define (double_procedure f)
  (lambda (x) (f (f x))))

(define (compose_procedure f g)
  (lambda (x) (f (g x))))

(define (repeat_procedure f k)
  (if (= k 1)
      f
      (compose_procedure f (repeat_procedure f (- k 1)))))
```

1.44 用上面的抽象过程来做n次平滑。

平滑函数的定义如下：

```
(define (smooth f)
  (lambda (x)
    (/ (+ (f x) (f (+ x dx)) (f (- x dx))) 3.0)))
```

那么n次平滑的定义应该为：

```
(define (smooth_n_times n)
  (repeat_procedure smooth n))
```

调用方法为`((smooth_n_times N) target_func)`。注意这样的n次平滑定义是错误的：

```rkt
(define (smooth_n_times f n)
  (repeat_procedure (smooth f) n))
```

这样调用方法是`(smooth_n_times target_func N)`，代换展开的结果是`(smooth f(smooth (f val)))`。而需要的是`(smooth (smooth f)))(val)`。仔细理解两者的差别。

1.46 抽象过程iterative-improve。它接受两个参数：是否足够好，改进方法。返回一个过程能够接受guess，返回答案。

```rkt
(define (iterative_improve good_enough? improve)
  (lambda (x)
    (if (good_enough? x)
        x
        ((iterative_improve good_enough? improve) (improve x)))))
```

用高阶抽象，解决问题一下子就上升了一个层次：只需要关注解决方法，而无需关注问题本身。并且这个方法可以很容易应用到具体问题。

比如用上面的过程来求解`sqrt(z)`:

```rkt
(define (sqrt z)
  ((iterative_improve (lambda (x) (real_equal? (* x x) z))
                     (lambda (x) (average x (/ z x)))) 1.0))
```